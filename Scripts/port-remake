#!/bin/csh

switch ( $#argv )
case    0:
    breaksw
case    1:
    echo $1 | fgrep -q /
    if ( $status == 0 ) then
	cd /usr/wip/$1
    else
	cd /usr/wip/*/$1
    endif
    breaksw
default:
    printf "Usage: $0 [[category/]dir]\n"
    exit 1
endsw

alias pause 'printf "Press return to continue..."; set junk=$<'

switch(`uname`)

# Deinstall as late as possible to minimize uninstalled time
case    'FreeBSD':
    printf "Remove distfile[s]? y/[n] "
    if ( "$<" == 'y' ) then
	make distclean
    else
	make clean
    endif
    make makesum
    
    printf "Regenerate plist? y/[n] "
    if ( "$<" == 'y' ) then
	make clean
	make
	make makeplist > pkg-plist.new
	vi pkg-plist.new
	printf "Diff:\n"
	if ( ! { diff -u pkg-plist pkg-plist.new } ) then
	    printf "Commit? y/[n] "
	    if ( "$<" == 'y' ) then
		mv -f pkg-plist.new pkg-plist
	    else
		exit 0
	    endif
	    make clean
	else
	    printf "No changes to pkg-plist.\n"
	    rm -f pkg-plist.new
	endif
    endif
    make deinstall
    make reinstall
    if ( $status == 0 ) then
	cd ../..
	printf "Updating repo...\n"
	svn up
	printf "Comitting changes...\n"
	svn ci -m 'Auto-updated by remake'
    endif
    breaksw

case    'Darwin':
    port -f uninstall
    port clean --all
    
    # Remove checksum(s) from Portfile
    # Find "checksums" in field 1, and remove all lines ending with '\'.
    awk ' { if ( $1 == "checksums" ) { while ( $NF == "\\" ) getline; } else { print $0 }}' Portfile >! temp
    /bin/mv -f Portfile Portfile.bak
    /bin/mv temp Portfile
    sleep 2     # port complains that Portfile is from future
    
    # Append new checksums to Portfile
    # Find "checksums" in field 1, then print all lines ending with '\'
    # and the one after.
    port -d checksum | & awk ' { if ( $1 == "checksums" ) { while ( $NF == "\\" ) { print $0; getline; } print $0; } }' >> Portfile
    sleep 2     # port complains that Portfile is from future
    
    port install
    if ( $status == 0 ) then
	cd ../../..
	./update-web-tree
	update-jb-ports
    endif
    breaksw
endsw

